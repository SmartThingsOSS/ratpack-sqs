buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    }
}

plugins {
    id "com.github.spotbugs" version "5.0.6" apply false
}

def jvmEncoding = java.nio.charset.Charset.defaultCharset().name()
if (jvmEncoding != "UTF-8") {
    throw new IllegalStateException("Build environment must be UTF-8 (it is: $jvmEncoding) - add '-Dfile.encoding=UTF-8' to the GRADLE_OPTS environment variable ")
}

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("Must be built with Java 8 or higher")
}

group 'smartthings'
version = rootProject.file('version.txt').text.trim()

apply plugin: 'base'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'
apply from: rootProject.file('gradle/publish.gradle')
apply from: rootProject.file('gradle/convention.gradle')

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked"
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation "io.ratpack:ratpack-core:${ratpackVersion}"
    implementation "io.ratpack:ratpack-guice:${ratpackVersion}"
    implementation "io.github.resilience4j:resilience4j-ratpack:0.10.0"
    implementation "io.github.resilience4j:resilience4j-circuitbreaker:0.10.0"
    implementation "io.github.resilience4j:resilience4j-metrics:0.10.0"
    implementation "io.github.resilience4j:resilience4j-prometheus:0.10.0"
    implementation platform("software.amazon.awssdk:bom:${awsSdkV2Version}")
    api "software.amazon.awssdk:sqs"
    implementation "software.amazon.awssdk:sns"
    implementation "software.amazon.awssdk:sts"
    implementation "software.amazon.awssdk:auth"

    testImplementation "org.objenesis:objenesis:2.6"
    testImplementation "org.spockframework:spock-core:1.0-groovy-2.4"
    testImplementation "io.ratpack:ratpack-groovy-test:${ratpackVersion}"
    testImplementation "cglib:cglib-nodep:3.2.4"
    testRuntimeOnly "org.slf4j:slf4j-api:${slf4jVersion}"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.1.7"
}

test {
    if (project.hasProperty('awsEndpointUrl')) {
        systemProperty 'awsEndpointUrl', awsEndpointUrl
    }
}

